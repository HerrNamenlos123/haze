
__c__("#include <stdio.h>");

namespace ffi {
    export extern C noemit printf(fmt: ccstr, ...) :: final;
}

namespace fmt {
    export struct FormatArgs {
        formatSpecifier: str = "";
    }

    export opaque struct StringWriter {
        buffer: mut MemoryBuffer | none;
        written: int = 0;

        constructor(buffer?: mut MemoryBuffer) {
            return StringWriter {
                buffer: buffer,
            };
        }

        mut write(value: str) {
            let buffer = this.buffer;
            if buffer {
                buffer.write(this.written, value);
            }
            this.written += value.length;
        }

        commit() {
            const buffer = this.buffer;
            if buffer {
                buffer.commit();
            }
        }

        string() {
            const buffer = this.buffer;
            if (buffer) {
                return buffer.string();
            }
            else {
                return "";
            }
        }
    }

    export format(values: ...) {
        let reader = StringWriter(none);
        for comptime value, i in values {
            format_to(reader, value, {});
        }
        let writer = StringWriter(MemoryBuffer(reader.written));
        for comptime value, i in values {
            format_to(writer, value, {});
        }
        writer.commit();
        return writer.string();
    }

    // export foo(allocator: Allocator, values: ...) {
    //     for comptime value, i in values {
    //         let a = value;
    //     }
    // }

    export formatWithAllocator(allocator: Allocator, values: ...) {
        let reader = StringWriter(none);
        for comptime value, i in values {
            format_to(reader, value, {});
        }
        let writer = StringWriter(MemoryBuffer(allocator, reader.written));
        for comptime value, i in values {
            format_to(writer, value, {});
        }
        writer.commit();
        return writer.toString();
    }


    export digitToStr(digit: int): str {
        assert(digit >= 0 && digit <= 9);
        let result: str = "";
        __c__("static const char* digits[] = { \"0\", \"1\", \"2\", \"3\", \"4\", \"5\", \"6\", \"7\", \"8\", \"9\" };");
        __c__("result = HZSTD_STRING(digits[digit], 1);");
        return result;
    }

    export print(args: ...) {
        for comptime arg in args {
            if comptime typeof(arg) == int || typeof(arg) == type<mut int> {
                const value = arg;
                // ffi.printf("%lld", arg);
                __c__("printf(\"%\"PRId64, value);");
            }
            else if typeof(arg) == cstr || typeof(arg) == ccstr {
                const s = arg;
                __c__("printf(\"%s\", s);");
                // ffi.printf("%s", arg);
            }
            else if typeof(arg) == str {
                const s = arg;
                __c__("fwrite(s.data, 1, s.length, stdout);");
                // ffi.printf("%s", arg);
            }
            else if typeof(arg) == type<str> {
                const s = arg;
                __c__("fwrite(s.data, 1, s.length, stdout);");
                // ffi.printf("%s", arg);
            }
            else if typeof(arg) == bool {
                if arg {
                    ffi.printf("true");
                }
                else {
                    ffi.printf("false");
                }
            }
            else if typeof(arg) == type<usize> {
                ffi.printf("%zu", arg);
            }
            else if typeof(arg) == type<real> || typeof(arg) == type<mut real> {
                ffi.printf("%f", arg);
            }
            else if typeof(arg) == type<u8> || typeof(arg) == type<u16> || typeof(arg) == type<u32> {
                const value = arg;
                __c__("printf(\"%\"PRIu8, value);");
            }
            else if typeof(arg) == type<u64> {
                const value = arg;
                __c__("printf(\"%\"PRIu64, value);");
            }
            else if typeof(arg) == type<i8> || typeof(arg) == type<i16> || typeof(arg) == type<i32> {
                const value = arg;
                __c__("printf(\"%\"PRId8, value);");
            }
            else if typeof(arg) == type<i64> {
                const value = arg;
                __c__("printf(\"%\"PRId64, value);");
            }
            else if typeof(arg) == type<Vec2> || typeof(arg) == type<Vec2> {
                ffi.printf("(%f, %f)", arg.x, arg.y);
            }
            else if typeof(arg) == type<Vec3> || typeof(arg) == type<Vec3> {
                ffi.printf("(%f, %f, %f)", arg.x, arg.y, arg.z);
            }
            else if typeof(arg) == type<Vec4> || typeof(arg) == type<Vec4> {
                ffi.printf("(%f, %f, %f, %f)", arg.x, arg.y, arg.z, arg.w);
            }
            else if typeof(arg) == type<fs.Error> {
                ffi.printf("%d", arg);
            }
            else if typeof(arg) == type<null> {
                ffi.printf("null");
            }
            else if typeof(arg) == type<none> {
                ffi.printf("none");
            }
            else {
                static_assert(false, "Type '" + typeof(arg).name + "' is not supported in fmt.print");
            }
        }
    }

    export println(args: ...) {
        for comptime arg, i in args {
            print(arg);
            if comptime i != args.length - 1 {
                print(" ");
            }
        }
        print("\n");
    }

    export format_to(writer: mut StringWriter, value: i64, args: FormatArgs) {
        let v = value;
        if v < 0 {
            writer.write("-");
            if v == -9223372036854775808 {
                writer.write("9223372036854775808");
                return;
            }
            v = -v;
        }

        // Find highest divisor
        let div: i64 = 1;
        while v / div >= 10 as i64 {
            div *= 10;
        }

        while div > 0 as i64 {
            let digit = v / div;
            writer.write(digitToStr(digit));
            v %= div;
            div /= 10;
        }
    }

    export format_to(writer: mut StringWriter, value: u64, args: FormatArgs) {
        let v = value;

        // Find highest divisor
        let div: u64 = 1;
        while v / div >= 10 as u64 {
            div *= 10;
        }

        while div > 0 as u64 {
            let digit = v / div;
            if (digit >= 0 as u64 && digit <= 9 as u64) {
                writer.write(digitToStr(digit));
            }
            else {
                assert(false);
            }
            v %= div;
            div /= 10;
        }
    }

    export format_to(writer: mut StringWriter, value: i8, args: FormatArgs) {
        format_to(writer, value as i64, args);
    }

    export format_to(writer: mut StringWriter, value: i16, args: FormatArgs) {
        format_to(writer, value as i64, args);
    }

    export format_to(writer: mut StringWriter, value: i32, args: FormatArgs) {
        format_to(writer, value as i64, args);
    }

    export format_to(writer: mut StringWriter, value: int, args: FormatArgs) {
        format_to(writer, value as i64, args);
    }

    export format_to(writer: mut StringWriter, value: str, args: FormatArgs) {
        writer.write(value);
    }
    
    export format_to(writer: mut StringWriter, value: u8, args: FormatArgs) {
        format_to(writer, value as u64, args);
    }

    export format_to(writer: mut StringWriter, value: u16, args: FormatArgs) {
        format_to(writer, value as u64, args);
    }

    export format_to(writer: mut StringWriter, value: u32, args: FormatArgs) {
        format_to(writer, value as u64, args);
    }

    export format_to(writer: mut StringWriter, value: usize, args: FormatArgs) {
        format_to(writer, value as u64, args);
    }
}